plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
    id 'java'
}

group = 'com.amtinez'
description = 'API Rest Users'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.mapstruct:mapstruct:1.5.1.Final'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    testImplementation 'org.mockito:mockito-inline:4.6.1'
    testImplementation 'net.bytebuddy:byte-buddy:1.12.12'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.12.12'
    testImplementation 'com.icegreen:greenmail:1.6.9'
    testRuntimeOnly 'org.hsqldb:hsqldb'
}

test {
    useJUnitPlatform()
}

sonarqube {
    properties {
        property 'sonar.projectKey', 'amtinez_api-rest-users'
        property 'sonar.organization', 'amtinez'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.exclusions', '**/ApiRestUsersApplication.java'
        property 'sonar.test.exclusions', '**/ApiRestUsersApplicationTests.java'
        property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/test/xml"
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/test/xml")
    }
}

task dockerStart(type: Exec) {
    description 'Start docker containers'
    workingDir "${workingDir}/docker"
    commandLine 'docker-compose', 'up', '-d'
}

task dockerStop(type: Exec) {
    description 'Stop docker containers'
    workingDir "${workingDir}/docker"
    commandLine 'docker-compose', 'stop'
}

task dockerRemove(type: Exec) {
    description 'Remove docker containers'
    workingDir "${workingDir}/docker"
    commandLine 'docker-compose', 'rm', '-f'
}
